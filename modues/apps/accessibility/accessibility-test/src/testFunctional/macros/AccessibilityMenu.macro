definition {

	@summary = "It activates the Accessibility Menu by Instance Settings or System Settings"
	macro activateAccessibilityMenu(portlet) {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = ${portlet});

		Click(
			key_itemName = "Accessibility",
			locator1 = "ListGroupItem#ITEM_TEXT");

		if (!(IsElementPresent(checkboxName = "Enable Accessibility Menu", locator1 = "Checkbox#ANY_CHECKBOX_CHECKED"))) {
			Check.checkNotVisible(
				checkboxName = "Enable Accessibility Menu",
				locator1 = "Checkbox#ANY_CHECKBOX");

			SystemSettings.saveConfiguration();
		}
	}

	@summary = "It activates the Accessibility Menu By Site Settings"
	macro activateAccessibilityMenuBySiteSettings(siteURLKey = null) {
		Site.openSiteSettingsAdmin(siteURLKey = ${siteURLKey});

		Click(
			key_itemName = "Accessibility",
			locator1 = "ListGroupItem#ITEM_TEXT");

		if (!(IsElementPresent(checkboxName = "Enable Accessibility Menu", locator1 = "Checkbox#ANY_CHECKBOX_CHECKED"))) {
			Check.checkNotVisible(
				checkboxName = "Enable Accessibility Menu",
				locator1 = "Checkbox#ANY_CHECKBOX");
		}

		SystemSettings.saveConfiguration();
	}

	@summary = "It asserts that the user can access the Accessibility Menu modal"
	macro assertCanAccessAccessibilityMenuModal() {
		KeyPress(
			locator1 = "//body",
			value1 = "\TAB");

		KeyPress(
			key_link = "Skip to Main Content",
			locator1 = "AccessibilityMenu#QUICK_LINK",
			value1 = "\TAB");

		KeyPress(
			key_link = "Open Accessibility Menu",
			locator1 = "AccessibilityMenu#QUICK_LINK",
			value1 = "\ENTER");

		AssertElementPresent(
			key_modal_title = "Accessibility Menu",
			locator1 = "AccessibilityMenu#MODAL_TITLE");
	}

	@summary = "It asserts that the user can access the Accessibility Menu modal by the user's profile menu"
	macro assertCanAccessAccessibilityMenuModalByTheUserProfileMenu() {
		Navigator.openURL();

		UserBar.gotoDropdown();

		ClickNoError(
			key_dropdownItem = "Accessibility Menu",
			locator1 = "UserBar#USER_AVATAR_DROPDOWN_ITEM");

		AssertElementPresent(
			key_modal_title = "Accessibility Menu",
			locator1 = "AccessibilityMenu#MODAL_TITLE");
	}

	@summary = "It asserts that the user cannot access the Accessibility Menu"
	macro assertCannotAccessAccessibilityMenuModal() {
		KeyPress(
			locator1 = "//body",
			value1 = "\TAB");

		KeyPress(
			key_link = "Skip to Main Content",
			locator1 = "AccessibilityMenu#QUICK_LINK",
			value1 = "\TAB");

		AssertElementNotPresent(
			key_link = "Open Accessibility Menu",
			locator1 = "AccessibilityMenu#QUICK_LINK");
	}

	@summary = "It asserts that the user cannot access the Accessibility Menu by the user's profile menu"
	macro assertCannotAccessAccessibilityMenuModalByTheUserProfileMenu() {
		Navigator.openURL();

		UserBar.gotoDropdown();

		AssertElementNotPresent(
			key_modal_title = "Accessibility Menu",
			locator1 = "AccessibilityMenu#MODAL_TITLE");
	}

	@summary = "It deactivates the Accessibility Menu"
	macro deactivateAccessibilityMenu(portlet = null) {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = ${portlet});

		Click(
			key_itemName = "Accessibility",
			locator1 = "ListGroupItem#ITEM_TEXT");

		if (!(IsElementPresent(checkboxName = "Enable Accessibility Menu", locator1 = "Checkbox#ANY_CHECKBOX_CHECKED"))) {
			Check.checkNotVisible(
				checkboxName = "Enable Accessibility Menu",
				locator1 = "Checkbox#ANY_CHECKBOX");

			SystemSettings.saveConfiguration();
		}
	}

	@summary = "It deactivates the Accessibility Menu by site settings"
	macro deactivateAccessibilityMenuBySiteSettings() {
		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings");

		Click(
			key_itemName = "Accessibility",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Uncheck.uncheckNotVisible(
			checkboxName = "Enable Accessibility Menu",
			locator1 = "Checkbox#ANY_CHECKBOX");

		SystemSettings.saveConfiguration();
	}

	@summary = "It disables a specific option in the Accessibility menu"
	macro disableSpecificOption(key_toggleSwitchLabel = null) {
		AccessibilityMenu.openAccessibilityMenu();

		Uncheck.uncheckToggleSwitch(
			key_toggleSwitchLabel = ${key_toggleSwitchLabel},
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
	}

	@summary = "Default summary"
	macro enableSpecificOption(key_toggleSwitchLabel = null) {
		AccessibilityMenu.openAccessibilityMenu();

		Check.checkToggleSwitch(
			key_toggleSwitchLabel = ${key_toggleSwitchLabel},
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
	}

	@summary = "Keyboard focus on the 2nd quick link item, Accessibility Menu"
	macro goToAccessibilityMenuQuickLink() {
		KeyPress(
			locator1 = "//body",
			value1 = "\TAB");

		KeyPress(
			key_link = "Skip to Main Content",
			locator1 = "AccessibilityMenu#QUICK_LINK",
			value1 = "\TAB");
	}

	@summary = "It opens the Accessibility Menu"
	macro openAccessibilityMenu() {
		AccessibilityMenu.goToAccessibilityMenuQuickLink();

		KeyPress(
			key_link = "Open Accessibility Menu",
			locator1 = "AccessibilityMenu#QUICK_LINK",
			value1 = "\ENTER");
	}

	@summary = "It resets the Accessibility Menu option to its default options"
	macro resetDefaultValues(portlet = null) {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = ${portlet});

		Click(
			key_itemName = "Accessibility",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Click(
			key_text = "Accessibility Menu",
			locator1 = "Icon#ANY_VERTICAL_ELLIPSIS");

		Click(
			key_menuItem = "Reset Default Values",
			locator1 = "MenuItem#DROPDOWN_MENU_ITEM");
	}

}